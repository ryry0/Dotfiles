# Zsh completion settings
autoload -U compinit && compinit
zmodload -i zsh/complist		# allows higlighting and colors in menus

# Completion functions
# set completions for jump and unmark
compctl -K _completemarks jump
compctl -K _completemarks unmark

# Completion styles
zstyle ':completion:*:*:*:*:*' menu select	# turns on colors in selection
eval $(dircolors) # set colors for ls
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}		# ls colors

# match uppercase from lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# use /etc/hosts and known_hosts for hostname completion
[ -r /etc/ssh/ssh_known_hosts ] && _global_ssh_hosts=(${${${${(f)"$(</etc/ssh/ssh_known_hosts)"}:#[\|]*}%%\ *}%%,*}) || _ssh_hosts=()
[ -r ~/.ssh/known_hosts ] && _ssh_hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[\|]*}%%\ *}%%,*}) || _ssh_hosts=()
[ -r /etc/hosts ] && : ${(A)_etc_hosts:=${(s: :)${(ps:\t:)${${(f)~~"$(</etc/hosts)"}%%\#*}##[:blank:]#[^[:blank:]]#}}} || _etc_hosts=()
[ -r ~/.ssh/config ] && _ssh_config=($(cat ~/.ssh/config | sed -ne 's/Host[=\t ]//p')) || _ssh_config=()
hosts=(
  "$_global_ssh_hosts[@]"
  "$_ssh_hosts[@]"
  "$_etc_hosts[@]"
  "$_ssh_config[@]"
  "$HOST"
  localhost
)
zstyle ':completion:*:hosts' hosts $hosts
